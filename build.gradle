import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    id "org.jetbrains.intellij" version "0.4.11"
    id 'com.palantir.git-version' version "0.11.0"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'
apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'

intellij {
    version ideaVersion
    pluginName 'PHPUnit Enhancement'
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            'CSS',
            'java-i18n',
            'properties',
            'yaml',
            'JavaScriptLanguage',
            "java"
    ]
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

patchPluginXml {
    sinceBuild '192.6603'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

group 'de.espend.idea.php.phpunit'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '5.5.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
